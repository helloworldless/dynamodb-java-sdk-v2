plugins {
    id 'java'
    id 'application'
}

group = 'com.davidagood'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
        url = 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
}

ext {
    lombokVersion = '1.18.20'
    mapstructVersion = '1.4.2.Final'
}

dependencies {
    implementation 'com.google.guava:guava:30.1.1-jre'

    implementation platform('software.amazon.awssdk:bom:2.17.24')
    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'software.amazon.awssdk:dynamodb-enhanced'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.4'

    implementation 'de.huxhorn.sulky:de.huxhorn.sulky.ulid:8.2.0'

    runtime 'ch.qos.logback:logback-classic:1.2.5'

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    testCompile 'org.assertj:assertj-core:3.20.2'

    testCompile 'com.amazonaws:DynamoDBLocal:1.13.6'
}

application {
    mainClassName = 'com.davidagood.awssdkv2.dynamodb.App'
}

test {
    useJUnitPlatform()
}

def nativeDepsDirForDynamoDBLocal = "${buildDir}/libs"

task copyNativeDepsForDynamoDBLocal(type: Copy) {
    from(configurations.compile + configurations.testCompile) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into nativeDepsDirForDynamoDBLocal
}

test {
    dependsOn copyNativeDepsForDynamoDBLocal
    systemProperty "java.library.path", nativeDepsDirForDynamoDBLocal
}
