plugins {
    id 'java'
    id 'application'
}

group = 'com.davidagood'
version = '1.0.0'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    jcenter()
    maven {
        url = 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
}

ext {
    lombokVersion = '1.18.16'
    mapstructVersion = '1.4.1.Final'
}

dependencies {
    implementation 'com.google.guava:guava:29.0-jre'

    implementation platform('software.amazon.awssdk:bom:2.15.0')
    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'software.amazon.awssdk:dynamodb-enhanced'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.0'

    runtime 'ch.qos.logback:logback-classic:1.2.3'

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    testCompile 'org.assertj:assertj-core:3.18.1'

    testCompile 'com.amazonaws:DynamoDBLocal:1.13.6'
}

application {
    mainClassName = 'com.davidagood.awssdkv2.dynamodb.App'
}

test {
    useJUnitPlatform()
}

def nativeDepsDirForDynamoDBLocal = "${buildDir}/libs"

task copyNativeDepsForDynamoDBLocal(type: Copy) {
    from(configurations.compile + configurations.testCompile) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into nativeDepsDirForDynamoDBLocal
}

test {
    dependsOn copyNativeDepsForDynamoDBLocal
    systemProperty "java.library.path", nativeDepsDirForDynamoDBLocal
}
